#define LOG(s,x...) {char buf[100]; sprintf(buf,s,x); Serial.println(buf);} //somehow you can't show unions... maybe more bugs

#include "LEDs.h"
#include "RAM.h"
#include "Switches.h"
#include "CPU.h"

byte program[] = {   //4k basic 3.2 bootstrap loader
  //LOOP
  0x21,0xAE,0x0F,   //lxi     h,0fae h    ;4K BASIC V3.2
  0x31,0x12,0x00,   //lxi     sp,STACK    ;init SP so a RET jumps to loop
  0xDB,0x00,        //in      00h         ;get sio status
  0x0F,             //rrc                 ;new byte available?
  // 0x76,//HALT
  0xD8,             //rc                  ;no (jumps back to loop)
  0xDB,0x01,        //in      01h         ;get the byte
  0xBD,             //cmp     l           ;new byte = leader byte? AE
  0xC8,             //rz                  ;yes (jumps back to loop)
  0x2D,             //dcr     l           ;not leader, decrement address
  0x77,             //mov     m,a  = mov [hl],a       ;store the byte (reverse order)
  0xC0,             //rnz                 ;loop until L = 0
  0x76, //HALT 0xE9,             //pchl                ;jump to code just downloaded
  //STACK
  0x03,0x00        //dw LOOP
};

//TODO DEBUGGER MAKEN IN STEP FUNCTION




//byte program[] = {   //Serial echo with interrupt PART 1
//  0x31,0x00,0x01,   //lxi     sp,0100h
//  0x3E,0x01,        //mvi     a,01h
//  0xD3,0x00,        //out     00h
//  0xFB,             //ei
//  //LOOP
//  0x00,             //nop
//  0x00,
//  0x00,
//  0xC3,0x08,0x00    //jmp LOOP
//};
//
//byte hook[] = { //@38h  - Serial echo with interrupt PART 2
//  0xF5,             //push a
//  0xDB,0x01,        //in 1
//  0xD3,0x01,        //out 1
//  0xF1,             //pop a
//  0xFB,             //ei
//  0xC9              //ret
//};

//byte program[] = {   //Serial echo 
//  0xdb,0x00,          //inp 00h ; a = Serial.available()
//  0x0f,               //rrc     ; shift right
//  0xda,0x00,0x00,     //jc 0    ; if carry jump to begin
//  0xdb,0x01,          //inp 01h ; a = Serial.read();
//  0xd3,0x01,          //out 01h ; Serial.write(a)
//  0xc3,0x00,0x00      //jmp 0   ; jmp 0
//};

//byte program[] = { //show sense switches on high part of address leds
//  0xdb,0xff,   //in 0xff (sense switches)
//  0x57,        //mov d,a
//  0x1a,        //ldax d
//  0x1a,
//  0x1a,
//  0x1a,
//  0xc3,0x00,0x00 //jmp 0
//};
  
//byte program[] = { //killbits
//  0x21,0x00,0x00,
//  0x16,0x80,
//  0x01,0x00,0x44,
//  0x1a,
//  0x1a,
//  0x1a,
//  0x1a,
//  0x09,
//  0xd2,0x08,0x00,
//  0xdb,0xff,
//  0xaa,
//  0x0f,
//  0x57,
//  0xc3,0x08,0x00 };

void load(byte *program, int s, int offset = 0) {
  LOG("Loading program %d bytes",s);
  for (uint16_t i=0; i<s; i++) {
    CPU.pc = i+offset;
    CPU.store(program[i]);
  }
}

void setup() { 
  Serial.begin(9600);

  load(program,sizeof(program),0);
//  load(hook,sizeof(hook),0x38);
  
  stop();
  examine(); //set pc to value of switches and show data on that address
}

void loop() {
  Switches.read();
  
  if (Switches.onRun()) run();
  if (Switches.onStop()) stop();
  if (Switches.onSingleStep()) singleStep();
  if (Switches.onExamine()) examine();
  if (Switches.onExamineNext()) examineNext();
  if (Switches.onDeposit()) deposit();
  if (Switches.onDepositNext()) depositNext();
  if (Switches.onReset()) reset();
  if (Switches.onClear()) clear();
  if (Switches.onAux1()) aux1();
  if (Switches.onAux2()) aux2();
  
  if (!CPU.wait) {
    CPU.step();
  }

  if (Switches.isDown(Switches.AUX1) || Switches.isDown(Switches.AUX1_)) {
    CPU.printInstruction();
    CPU.printRegisters();
  }
    
  LEDs.setFlag(LEDs.WAIT,CPU.wait);
  LEDs.setFlag(LEDs.M1,CPU.m1);
  LEDs.setFlag(LEDs.MEMR,CPU.memr);
  LEDs.setFlag(LEDs.INTE,CPU.flags & CPU.IF); //IE / interrupts enabled
  LEDs.setFlag(LEDs.INP,CPU.opcode == 0xDB);
  LEDs.setFlag(LEDs.OUT,CPU.opcode == 0xD3);
  LEDs.write();
  
//  delay(100);
}

void examine() { 
  CPU.pc = Switches.ax; //examine updates the program counter
  LEDs.data = CPU.fetch();
}

void examineNext() {
  CPU.pc++;
  LEDs.data = CPU.fetch();
}

void deposit() {
  CPU.store(Switches.ax);
  LEDs.data = CPU.fetch();
}

void depositNext() {
  CPU.pc++;
  CPU.store(Switches.ax);
  LEDs.data = CPU.fetch();
}

void reset() {
  CPU.pc = 0;
  LEDs.data = CPU.fetch();
}

void clear() {
  Serial.print("clearing 4kb of memory... (excl. bootloader)");
  for (int i=sizeof(program)+1; i<0x1000; i++) {
    RAM.write(i,0);
  }
  Serial.println("done");
}

void singleStep() {
  CPU.step();
}

void run() {
  CPU.wait = false;
}

void stop() {
  CPU.wait = true;
}

void aux1() {

}

void aux2() {
  char buf[10];
  char line[17];
  line[16] = '\0';
  for (int i=0x0f00; i<0x1000; i+=16) {
    //prefix
    sprintf(buf,"%04X: ",i); 
    Serial.print(buf);

    //read + print hex
    for (int j=0; j<16; j++) {
      byte b = RAM.read(i+j);
      sprintf(buf,"%02X ",b);
      Serial.print(buf);
      line[j] = (b>=32 && b<127) ? b : '.';
    }
    Serial.println(line);
  }
}
