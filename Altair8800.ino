#include "SoftwareSerial.h"

char printbuf[20];

SoftwareSerial softserial(2,3);

#define LOG(s,x...) { sprintf(printbuf,s,x); Serial.println(printbuf); softserial.println(printbuf); }

#include <SPI.h>
#include <SD.h>
#include "LEDs.h"
#include "RAM.h"
#include "Switches.h"
#include "CPU.h"
#include "ROM.h"
#include "Debugger.h"


void setup() { 

  Serial.begin(9600);
  softserial.begin(9600);
  stop();
  examine(); //set pc to value of switches and show data on that address
  ROM.loadBasicFromSD();
}

void loop() {
  Switches.read();
  
  if (Switches.onRun()) run();
  if (Switches.onStop()) stop();
  if (Switches.onSingleStep()) singleStep();
  if (Switches.onExamine()) examine();
  if (Switches.onExamineNext()) examineNext();
  if (Switches.onDeposit()) deposit();
  if (Switches.onDepositNext()) depositNext();
  if (Switches.onReset()) reset();
  if (Switches.onClear()) clear();
  if (Switches.onAux1()) aux1();
  if (Switches.onAux2()) aux2();
  
  if (!CPU.wait && debug.breakpoint == CPU.pc) {
    Serial.print(F("Hit breakpoint at "));
    LOG("%04x",CPU.pc);
    stop();
  }

  if (!CPU.wait) {
    // debug.unassemble_addr = CPU.pc;
    // debug.unassemble();
    CPU.step();
    // debug.registersAndFlags();
  }

  if (debug.enabled) {
    debug.update();
  }
    
  LEDs.setFlag(LEDs.WAIT,CPU.wait);
  LEDs.setFlag(LEDs.M1,CPU.m1);
  LEDs.setFlag(LEDs.MEMR,CPU.memr);
  LEDs.setFlag(LEDs.INTE,CPU.flags & CPU.IF); //IE / interrupts enabled
  LEDs.setFlag(LEDs.INP,CPU.opcode == 0xDB);
  LEDs.setFlag(LEDs.OUT,CPU.opcode == 0xD3);
  LEDs.write();
  
//  delay(100);
}

void examine() { 
  CPU.pc = Switches.ax; //examine updates the program counter
  LEDs.data = CPU.fetch();
}

void examineNext() {
  CPU.pc++;
  LEDs.data = CPU.fetch();
}

void deposit() {
  CPU.store(Switches.ax);
  LEDs.data = CPU.fetch();
}

void depositNext() {
  CPU.pc++;
  CPU.store(Switches.ax);
  LEDs.data = CPU.fetch();
}

void reset() {
  CPU.pc = 0;
  LEDs.data = CPU.fetch();
}

void clear() {
  
}

void singleStep() {
  CPU.step();
}

void run() {
  CPU.wait = false;
  debug.stop();
}

void stop() {
  CPU.wait = true;
  debug.start();
}

void aux1() {

}

void aux2() {
  ROM.loadBasicFromSD();
}
