#include <SPI.h>
#include <SD.h>
//#include <SoftwareSerial.h>

//SoftwareSerial softserial(2,3);
const byte SD_SELECT = 4;
const byte CLOCK = 5;
const byte LED_SELECT = 8;
const byte LED_DATA = 9;
const byte SW_SELECT = 6;
const byte SW_DATA = 7;

extern "C" {
  #include "i8080.h"
}

enum State { 
  HLDA, WAIT, INTE, PROT, MEMR, INP, 
  M1, OUT, HLTA, STACK, WO, INT };

struct {
  int address;
  byte data;
  byte state;
} bus;

#include "RAM.h"
#include "Panel.h"
#include "DiskController.h"
#include "IO.h"
#include "HAL.h"
#include "Loader.h"

void setup() {
  Serial.begin(115200);
//  softserial.begin(9600);
  SPI.begin();
  pinMode(LED_SELECT, OUTPUT);
  pinMode(LED_DATA, OUTPUT);
  pinMode(CLOCK, OUTPUT);
  digitalWrite(LED_SELECT, HIGH); //disable
  pinMode(SW_SELECT, OUTPUT);
  pinMode(SW_DATA, INPUT);
  digitalWrite(SW_SELECT, HIGH); //disable
  bus.state = 0x2; //all flags off except STOP
  bus.address = 0;
  bus.data = 0;
}

void loop() {
  bitClear(bus.state,MEMR); //flag set by RAM.readByte()
  bitClear(bus.state,M1);  //flag set by step()
  bitClear(bus.state,OUT); //flag set by IO.write()
  bitClear(bus.state,INP); //flag set by IO.read()
    
  panel.readSwitches();
  
  if (panel.onStop()) bitSet(bus.state,WAIT);
  if (panel.onRun()) bitClear(bus.state,WAIT);
  if (panel.onSingleStep()) i8080_instruction();
  if (panel.onExamine()) examine(panel.ax);
  if (panel.onExamineNext()) examine(i8080_pc()+1);
  if (panel.onDeposit()) deposit(i8080_pc(),panel.ax);
  if (panel.onDepositNext()) deposit(i8080_pc()+1,panel.ax);
  if (panel.onReset()) examine(0);
  if (panel.onAux1Up()) loader.loadKillbits();
  if (panel.onAux1Down()) loader.loadFile("4KBAS32.BIN", 0);
  if (panel.onAux2Down()) {
    loader.loadFile("88DSKROM.BIN", 0xff00);
    disk.mount(disk.disk1,"cpm63k.dsk");
    disk.mount(disk.disk2,"zork.dsk");
    examine(0xff00);
  }
  
  if (!bitRead(bus.state,WAIT)) i8080_instruction();
  
  panel.writeLEDs();
}

void examine(uint16_t address) {
  i8080_jump(address); //set program counter
  RAM::readByte(address);
}

void deposit(uint16_t address, byte data) {
  i8080_jump(address); //set program counter
  RAM::writeByte(address,data);
}

